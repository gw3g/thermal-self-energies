size 18.5 12.5
set lwidth 0.06
set cap round
set font psncsi
set hei 0.6

amove 2 2
begin graph
  size 16 10 fullsize

  xlabels font psncsr hei 0.5
  xticks length 0.25
  ylabels font psncsr hei 0.5
  yticks length 0.25

  xaxis min -1.2 max 1.6 dticks 0.5
  xtitle "q^2/m_D^2" hei 0.5 font psncsi dist 0.5
  yaxis min -.1 max 1.6 dticks .5
  !ynames 1 "" "" 1h0 "" "" 100 
  ytitle "\omega^2/m_D^2" hei 0.6 font psncsi dist 0.1

  let d4 = x               from 0 to 1.9

  data "omega(q), htl.csv" comment # d11 = c2, c1
  data "omega(q), htl.csv" comment # d12 = c3, c1
  data "omega(q), qed.csv" comment # d13 = c2, c1
  data "omega(q), qed.csv" comment # d14 = c3, c1
  data "omega(q), qcd.csv" comment # d15 = c2, c1
  data "omega(q), qcd.csv" comment # d16 = c3, c1

  d4 line             color 0.5 lstyle 8 lwidth 0.06
  d11 line            color #EB811B lstyle 8 lwidth 0.08     ! orange
  d12 line            color #EB811B lstyle 8 lwidth 0.08     ! - htl
  d13 line            color #0057AE lstyle 1 lwidth 0.12     ! blue
  d14 line            color #0057AE lstyle 4 lwidth 0.08     ! - photon
  d15 line            color #14B03D lstyle 1 lwidth 0.12     ! green
  d16 line            color #14B03D lstyle 4 lwidth 0.08     ! - gluon

end graph

set hei 0.5
begin key
  compact
  position br
  nobox
  offset 0.9 0.9
  text "HTL" line color #EB811B lstyle 8 lwidth 0.08
  text "photon" line color #0057AE lstyle 1 lwidth 0.12
  text "gluon" line color #14B03D lstyle 1 lwidth 0.12
end key


